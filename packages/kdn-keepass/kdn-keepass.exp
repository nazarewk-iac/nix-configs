#!/usr/bin/env expect

proc log {logger message} {
  send_user -- "$logger: $message\n"
}

lassign $argv dbpath passpath
spawn keepassxc --pw-stdin "$dbpath"
set keepassxc_spawn_id $spawn_id
set keepassxc_pid [exp_pid]
log kdn-keepassxc "started as PID=$keepassxc_pid SPAWN_ID=$spawn_id"

if {[info exists ::env(NOTIFY_SOCKET)]} {
  system systemd-notify --pid="$keepassxc_pid"
  log kdn-keepassxc:systemd "MAINPID set to $keepassxc_pid"
}

set password [exec pass "$passpath" 2>/dev/null]
expect {
  "Another instance of KeePassXC is already running." { exit 1 }
  "Database password: " {
    send "$password\n"
    send_user "\n"
    log kdn-keepassxc "password submitted"
  }
  timeout { exit 1 }
}
log kdn-keepassxc "wait to be ready"
if {[info exists ::env(SWAYSOCK)]} {
  spawn bash -c { swaymsg -t subscribe -m '["window"]' | gron --stream --monochrome }
  expect -i $spawn_id {*app_id = "org.keepassxc.KeePassXC";} { close -i $spawn_id }
  log sway {"org.keepassxc.KeePassXC" window created}
}
if {[info exists ::env(NOTIFY_SOCKET)]} {
  system systemd-notify --ready
  log kdn-keepassxc:systemd "ready signal sent"
}

interact -i $keepassxc_spawn_id
